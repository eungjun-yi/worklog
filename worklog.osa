-- For whitespace ap issue
on replace_chars(this_text, search_string, replacement_string)
	set AppleScript's text item delimiters to the search_string
	set the item_list to every text item of this_text
	set AppleScript's text item delimiters to the replacement_string
	set this_text to the item_list as string
	set AppleScript's text item delimiters to ""
	return this_text
end replace_chars

-- Tell what you are doing now

try
    set window_title to ""
    tell application "System Events"
        set frontApp to first application process whose frontmost is true
        set frontAppName to name of frontApp
        tell process frontAppName
            tell (1st window whose value of attribute "AXMain" is true)
                set window_title to value of attribute "AXTitle"
            end tell
        end tell
    end tell
on error errmsg number errnum
	-- Know Errors:
	-- File WinAppHelper wasn’t found. (-43)
	-- Microsoft Messenger got an error: Connection is invalid. (-609)
	-- Google Chrome got an error: AppleEvent timed out. (-1712)
	-- VirtualBoxVM got an error: every window doesn’t understand the count message. (-2700)
	-- The variable window_title is not defined. (-2753)
	if {-43, -609, -1704, -1708, -1712, -2700, -2753, -2741} does not contain errnum
		error errmsg
	end if
	set window_title to ""
end try

-- For variable not defined issue
try
	if window_title = missing value then set window_title to ""
on error
	set window_title to ""
end try

set theDate to (do shell script "date '+%Y/%m/%d-%H:%M'")
set theIp to do shell script "/sbin/ifconfig | grep 'inet.*broadcast' | awk '{print $2}'"
set theSsid to do shell script "/System/Library/PrivateFrameworks/Apple80211.framework/Versions/A/Resources/airport -I | grep -E '\\<SSID\\>' | awk '{print $2}'"

if theIp = "" then set theIp to "-"
if theSsid = "" then set theSsid to "-"

-- replace whitespace
set theSsid to replace_chars(theSsid, " ", "+")

-- <format-id> <ip> <ssid> <date> <appname> <window-title>
"a4 " & theIp & " " & theSsid & " " & theDate & " " & frontAppName & ": " & window_title
